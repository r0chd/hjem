{
  hjemModule,
  hjemTest,
  lib,
  formats,
  writeText,
}: let
  userHome = "/home/alice";
in
  hjemTest {
    name = "hjem-xdg-finix";
    nodes = {
      node1 = {
        imports = [hjemModule];

        services.mdevd.enable = true;

        users.groups.alice = {};
        users.users.alice = {
          isNormalUser = true;
          home = userHome;
          password = "";
        };

        hjem.linker = null;
        hjem.users = {
          alice = {
            enable = true;
            files = {
              "foo" = {
                text = "Hello world!";
              };
            };
            xdg = {
              cache = {
                directory = userHome + "/customCacheDirectory";
                files = {
                  "foo" = {
                    text = "Hello world!";
                  };
                };
              };
              config = {
                directory = userHome + "/customConfigDirectory";
                files = {
                  "bar.json" = {
                    generator = lib.generators.toJSON {};
                    value = {bar = "Hello second world!";};
                  };
                };
              };
              data = {
                directory = userHome + "/customDataDirectory";
                files = {
                  "baz.toml" = {
                    generator = (formats.toml {}).generate "baz.toml";
                    value = {baz = "Hello third world!";};
                  };
                };
              };
              state = {
                directory = userHome + "/customStateDirectory";
                files = {
                  "foo" = {
                    source = writeText "file-bar" "Hello fourth world!";
                  };
                };
              };

              mime-apps = {
                added-associations."text/html" = ["firefox.desktop" "zen.desktop"];
                removed-associations."text/xml" = ["thunderbird.desktop"];
                default-applications."text/html" = "firefox.desktop";
              };
            };
          };
        };
      };
    };

    testScript = ''
      machine.wait_until_succeeds("initctl cond get task/hjem-activate-alice/success")

      with subtest("XDG basedir spec files created by Hjem"):
        machine.succeed("[ -L ~alice/customCacheDirectory/foo ]")
        machine.succeed("grep \"Hello world!\" ~alice/customCacheDirectory/foo")
        machine.succeed("[ -L ~alice/customConfigDirectory/bar.json ]")
        machine.succeed("grep \"Hello second world!\" ~alice/customConfigDirectory/bar.json")
        machine.succeed("[ -L ~alice/customDataDirectory/baz.toml ]")
        machine.succeed("grep \"Hello third world!\" ~alice/customDataDirectory/baz.toml")
        # Same name as cache test file to verify proper merging
        machine.succeed("[ -L ~alice/customStateDirectory/foo ]")
        machine.succeed("grep \"Hello fourth world!\" ~alice/customStateDirectory/foo")

      with subtest("XDG mime-apps spec file created by Hjem"):
        machine.succeed("[ -L ~alice/customConfigDirectory/mimeapps.list ]")
        machine.succeed("grep \"text/xml\" ~alice/customConfigDirectory/mimeapps.list")

      with subtest("Basic test file for Hjem"):
        machine.succeed("[ -L ~alice/foo ]") # Same name as cache test file to verify proper merging
        machine.succeed("grep \"Hello world!\" ~alice/foo")
    '';
  }
